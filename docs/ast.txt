complete_command : list #';'
                 | list
                 ;
list             : list #';' pipe_sequence
                 |           pipe_sequence
                 ;
pipe_sequence    :                               simple_command
                 | pipe_sequence #'|' #linebreak simple_command
                 ;
simple_command   : cmd_prefix cmd_word cmd_suffix
                 | cmd_prefix cmd_word
                 | cmd_prefix
                 | cmd_name cmd_suffix
                 | cmd_name
                 ;
cmd_name         : WORD                   /* Apply rule 7a */
                 ;
cmd_word         : WORD                   /* Apply rule 7b */
                 ;
cmd_prefix       :            io_redirect
                 | cmd_prefix io_redirect
                 ;
cmd_suffix       :            io_redirect
                 | cmd_suffix io_redirect
                 |            WORD
                 | cmd_suffix WORD
                 ;
io_redirect      :           io_file
                 | IO_NUMBER io_file
                 |           io_here
                 | IO_NUMBER io_here
                 ;
io_file          : '<'       filename
                 | '>'       filename
                 | '>>'      filename
                 ;
filename         : WORD                      /* Apply rule 2 */
                 ;
io_here          : #'<<'     here_end
                 ;
here_end         : WORD                      /* Apply rule 3 */
                 ;
#newline_list    :               #NEWLINE
                 | #newline_list #NEWLINE
                 ;
#linebreak       : #newline_list
                 | /* empty */
                 ;
